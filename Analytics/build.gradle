plugins {

    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

/*

    FACEBOOK_APP_ID
    FACEBOOK_CLIENT_TOKEN
    FB_LOGIN_PROTOCOL_SCHEME

*/
def facebookApplicationId = System.getenv("FACEBOOK_APP_ID") ?: ""
def facebookClientToken = System.getenv("FACEBOOK_CLIENT_TOKEN") ?: ""
def facebookProtocolScheme = System.getenv("FB_LOGIN_PROTOCOL_SCHEME") ?: ""

Properties localProperties = new Properties()
File localPropertiesFile = rootProject.file('local.properties')

if (localPropertiesFile.exists()) {

    localProperties.load(localPropertiesFile.newDataInputStream())
}

if (facebookApplicationId == "") {

    facebookApplicationId = localProperties.getProperty('FACEBOOK_APP_ID', '')
}

if (facebookClientToken == "") {

    facebookClientToken = localProperties.getProperty('FACEBOOK_CLIENT_TOKEN', '')
}

if (facebookProtocolScheme == "") {

    facebookProtocolScheme = localProperties.getProperty('FB_LOGIN_PROTOCOL_SCHEME', '')
}

if (facebookApplicationId != "") {

    println(

            "Facebook :: The 'ANALYTICS_FACEBOOK_APP_ID' env. variable is available :: Hash = " +
            facebookApplicationId.hashCode()
    )
}

if (facebookClientToken != "") {

    println(

            "Facebook :: The 'FACEBOOK_CLIENT_TOKEN' env. variable is available :: Hash = " +
            facebookClientToken.hashCode()
    )
}

if (facebookProtocolScheme != "") {

    println(

            "Facebook :: The 'FB_LOGIN_PROTOCOL_SCHEME' env. variable is available :: Hash = " +
            facebookProtocolScheme.hashCode()
    )
}

android {

    compileSdk 36
    namespace 'com.redelf.analytics'

    defaultConfig {

        minSdk 28

        consumerProguardFiles "consumer-rules.pro"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        resValue "string", "facebook_app_id", facebookApplicationId
        resValue "string", "facebook_client_token", facebookClientToken
        resValue "string", "fb_login_protocol_scheme", facebookProtocolScheme
    }

    buildTypes {

        release {

            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'missing_rules.txt'
        }

        debug {

            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'missing_rules.txt'
        }
    }

    packagingOptions {

        resources {

            excludes += ['META-INF/*, LICENSE-2.0.txt, META-INF/DEPENDENCIES', 'META-INF/LICENSE',
                         'META-INF/LICENSE.txt', 'META-INF/license.txt', 'META-INF/NOTICE',
                         'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0',
                         'META-INF/*.kotlin_module', 'META-INF/*', 'LICENSE-2.0.txt',
                         'META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE.txt',
                         'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/USE_EXACT_ALARM']
        }

        jniLibs {

            excludes += ['META-INF/*']
        }
    }

    compileOptions {

        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {

        jvmTarget = '1.8'
    }

    lint {

        disable "Instantiatable"
        disable 'ContentDescription'
    }
}

dependencies {

    implementation project("${toolkit_context}:Main")

    testImplementation project("${toolkit_context}:Main")
    testImplementation project("${toolkit_context}:Test")

    androidTestImplementation project("${toolkit_context}:Main")
    androidTestImplementation project("${toolkit_context}:Test")

    implementation 'com.facebook.android:facebook-android-sdk:18.0.3'
}